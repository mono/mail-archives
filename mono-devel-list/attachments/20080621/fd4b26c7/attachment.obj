// Metadata version: v2.0.50215
.assembly extern mscorlib
{
  .publickeytoken = (B7 7A 5C 56 19 34 E0 89 )                         // .z\V.4..
  .ver 2:0:0:0
}
.assembly TailCall2
{
  .custom instance void [mscorlib]System.Runtime.CompilerServices.CompilationRelaxationsAttribute::.ctor(int32) = ( 01 00 08 00 00 00 00 00 ) 
  .hash algorithm 0x00008004
  .ver 0:0:0:0
}
.module TailCall2.exe
// MVID: {A224F460-A049-4A03-9E71-80A36DBBBCD3}
.imagebase 0x00400000
.file alignment 0x00000200
.stackreserve 0x00100000
.subsystem 0x0003       // WINDOWS_CUI
.corflags 0x00000001    //  ILONLY
// Image base: 0x02F20000

.class private abstract auto ansi sealed beforefieldinit TestClass
    extends [mscorlib]System.Object
{
    .class auto ansi sealed nested private Del
        extends [mscorlib]System.MulticastDelegate
    {
        .method public hidebysig specialname rtspecialname instance void .ctor(object 'object', native int 'method') runtime managed
        {
        }

        .method public hidebysig newslot virtual instance class [mscorlib]System.IAsyncResult BeginInvoke(int32 i, class [mscorlib]System.AsyncCallback callback, object 'object') runtime managed
        {
        }

        .method public hidebysig newslot virtual instance int32 EndInvoke(class [mscorlib]System.IAsyncResult result) runtime managed
        {
        }

        .method public hidebysig newslot virtual instance int32 Invoke(int32 i) runtime managed
        {
        }

    }

    .field private static class TestClass/Del del

    .method private hidebysig static int32 Add(int32 i) cil managed
    {
        .maxstack 8
        ldarg.0 
        brtrue.s ok
        ldc.i4.0 
        ret 
ok:
        ldsfld class TestClass/Del TestClass::del
        ldarg.0 
        ldc.i4.1 
        sub 
		tail.
        callvirt instance int32 TestClass/Del::Invoke(int32)
        ret 
    }

    .method public hidebysig static void Main(string[] Arguments) cil managed
    {
        .entrypoint
        .maxstack 8
		ldnull 
		ldftn int32 TestClass::Add(int32)
		newobj instance void TestClass/Del::.ctor(object, native int)
		stsfld class TestClass/Del TestClass::del
		ldsfld class TestClass/Del TestClass::del
        ldc.i4 0xfffff
        callvirt instance int32 TestClass/Del::Invoke(int32)
        call void [mscorlib]System.Console::WriteLine(int32)
        ret 
    }
}